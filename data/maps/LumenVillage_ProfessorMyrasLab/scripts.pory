mapscripts LumenVillage_ProfessorMyrasLab_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_0, 0 {
            removeobject(ProfMyrasLab_Rival_Intro_2)
            turnobject(LOCALID_PLAYER, DIR_NORTH)
        }
    ]
    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)
        if (var(VAR_INTRO_SCENE) >= 5) {
            clearflag(FLAG_HIDE_LAB_ASSISTANT)
            addobject(ProfMyrasLab_Assistant)
            setobjectxyperm(ProfMyrasLab_Assistant, 9, 8)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_INTRO_SCENE, 0 {
            setvar(VAR_INTRO_SCENE, 1)
            applymovement(LOCALID_PLAYER, moves(walk_up * 7))
            waitmovement(0)
            speakername(ProfMyraName)
            msgbox(format("Jirachi seems to be doing a bit better now, thanks to you, {PLAYER}."))
            playmoncry(SPECIES_JIRACHI, CRY_MODE_NORMAL)
            waitmoncry()
            msgbox(format("However, I think it's best if we get it some more potent medicine.\p"
                "I know you've done a lot already, but would you be willing to go to Solstice Town to pick it up?\p"
                "It's just the next town over.\p"
                "You can take the Pokémon I lent you, of course.\p"
                "The PokéMart attendant will have it ready for you at my request.\p"
                "Thank you, {PLAYER}. We'll see you when you get back!"), MSGBOX_AUTOCLOSE)
            setflag(FLAG_SYS_QUEST_MENU_GET)
            startquest(QUEST_1_MEDS_FOR_JIRACHI)
            openquestmenu()
            end
        }

        VAR_INTRO_SCENE, 3 {
            setvar(VAR_INTRO_SCENE, 4)
            addobject(ProfMyrasLab_Rival_Intro_2)
            applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark))
            waitmovement(0)
            playse(SE_PIN)
            playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
            delay(50)
            applymovement(LOCALID_PLAYER, moves(walk_up * 6))
            waitmovement(0)
            call(RivalName)
            msgbox(format("Professor Myra! I want a Pokémon too!\p"
                "{PLAYER} already got one! It's not fair that only {PLAYER} gets one!\p"
                "I'll treat it well! We'll get strong together! Please believe me!"))
            speakername(ProfMyraName)
            msgbox(format("Hmmm… These Pokémon were part of a special research assignment.\p"
                "They weren't meant for trainers - not yet.\p"
                "But I see it in your eyes… you're serious.\p"
                "You really want to prove you're ready, don't you?"))
            call(RivalName)
            msgbox(format("I've ALWAYS been ready!\p"
                "If {PLAYER} is out there, then I should be too!\p"
                "Please, Professor! I want to be a Pokémon Trainer!"))
            speakername(ProfMyraName)
            msgbox(format("…very well.\p"
                "But know this: once you take this step, there's no going back.\p"
                "This journey will challenge more than just your strength.\p"
                "Choose carefully."))
            call(RivalName)
            msgbox(format("Thank you, Professor Myra!!\p"
                "I promise I won't let you down!"), MSGBOX_AUTOCLOSE)
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox(format("{RIVAL} received a Pokémon!"), MSGBOX_AUTOCLOSE)
            waitfanfare()
            applymovement(ProfMyrasLab_Rival_Intro_2, moves(face_down))
            waitmovement(0)
            applymovement(ProfMyrasLab_Rival_Intro_2, moves(emote_exclamation_mark))
            playse(SE_PIN)
            waitmovement()
            call(RivalName)
            msgbox(format("{PLAYER}!\p"
                "I told you I'd get a Pokémon too!\p"
                "Now we're even. Don't think you'll stay one step ahead for long!\p"
                "…but, uh, I'm still glad we're doing this together.\p"
                "Even if I AM gonna beat you!\p"
                "Catch you later!"), MSGBOX_AUTOCLOSE)
            applymovement(LOCALID_PLAYER, ProfMyrasLab_Movement_PlayerWatchRivalExit)
            applymovement(ProfMyrasLab_Rival_Intro_2, ProfMyrasLab_Movement_RivalExit)
            waitmovement(0)
            playse(SE_DOOR)
            waitse()
            removeobject(ProfMyrasLab_Rival_Intro_2)
            fadedefaultbgm()
            applymovement(LOCALID_PLAYER, moves(walk_up))
            waitmovement(0)
            speakername(ProfMyraName)
            msgbox(format("Hi {PLAYER}!\p"
                "You came at just the right time.\p"
                "I ended up giving your friend a Pokémon.\p"
                "For the sake of being fair, I think you should keep the one I lent you earlier.\p"
                "After all, it looks like you two have already started bonding."))
            playfanfare(MUS_OBTAIN_ITEM)
            speakername(NULL)
            bufferleadmonspeciesname(STR_VAR_1)
            msgbox(format("{PLAYER} received the {STR_VAR_1}!"), MSGBOX_AUTOCLOSE)
            waitfanfare()
            msgbox(format("Would you like to give a nickname to {STR_VAR_1}?"), MSGBOX_YESNO)
            if (var(VAR_RESULT) == YES) {
                call(Common_EventScript_NameReceivedPartyMon)
            }
            speakername(ProfMyraName)
            msgbox(format("I see you've also retrieved the medicine from Solstice Town.\p"
                "Thank you for that, {PLAYER}.\p"
                "However, Jirachi has already left.\p"
                "I'm worried about it, since it was badly injured.\p"
                "You might not know this, but Jirachi is a very special Pokémon.\p"
                "Whoever those Team Eclipse strangers were, I don't think they had good intentions towards Jirachi.\p"
                "If it went anywhere… it must have returned to the place where you found it.\p"
                "You should go back to {COLOR RED}Starfall Clearing {COLOR DARK_GRAY}to look for it.\p"
                "If it chose to show itself to you, it might do so again.\p"
                "Please look for Jirachi, {PLAYER}, and make sure it is safe."), MSGBOX_AUTOCLOSE)
            updatequest(QUEST_1_MEDS_FOR_JIRACHI, VAR_MEDICINE_FOR_JIRACHI_STATE)
            end
        }
    ]
}

script RivalName {
    checkplayergender()
    if (var(VAR_RESULT) == MALE) {
        speakername("Selene")
    } else {
        speakername("Cassian")
    }
}

script ProfMyrasLab_EventScript_ProfMyra_Dialogue {
    if (var(VAR_INTRO_SCENE) < 4) {
        speakername(ProfMyraName)
        msgbox(format("Thank you for agreeing to get the medicine from Solstice Town for Jirachi.\p"
            "It's imperative we help it recover."), type=MSGBOX_NPC)
    } elif (var(VAR_INTRO_SCENE) == 4) {
        speakername(ProfMyraName)
        msgbox(format("Please look for Jirachi, {PLAYER}, and make sure it is safe."), type=MSGBOX_NPC)
    } else {
        speakername(ProfMyraName)
        msgbox(format("I hope your journey's off to a good start, {PLAYER}.\p"
        "Don't forget - you're not alone out there.\p"
        "Oh, and remember to register Pokémon in your Pokédex.\p"
        "The more you learn, the stronger you'll become.\p"
        "Safe travels, {PLAYER}."), type=MSGBOX_NPC)
    }
}

script ProfMyrasLab_EventScript_JirachiNPC {
    playmoncry(SPECIES_JIRACHI, CRY_MODE_NORMAL)
    waitmoncry()
}

script ProfMyrasLab_EventScript_Assistant_Dialogue {
    if (var(VAR_INTRO_SCENE) == 5) {
        applymovement(ProfMyrasLab_Assistant, moves(face_player))
        msgbox(format("Prof. Myra went out. I'm not sure where she went."), type=MSGBOX_NPC)
    } else {
        applymovement(ProfMyrasLab_Assistant, moves(face_player))
        msgbox(format("Prof. Myra is an astrologer. She's very interested in the stars.\p"
            "That's why she moved to Lumen Village - there's a lot less light pollution around here."), type=MSGBOX_NPC)
    }
}

movement ProfMyrasLab_Movement_RivalExit {
    walk_fast_right
    walk_fast_down * 7
}

movement ProfMyrasLab_Movement_PlayerWatchRivalExit {
    delay_16
    walk_in_place_faster_right
    delay_16
    walk_in_place_faster_down
}