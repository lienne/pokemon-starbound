mapscripts SolsticeTown_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setflag(FLAG_HIDE_RIVAL_IN_SOLSTICE_TOWN)
        removeobject(SolsticeTown_Rival)
    }
}

script SolsticeTown_EventScript_TownSign {
    lockall
    msgbox(format("Solstice Town\n"
        "“The balance between light and shadow”"))
    releaseall
}

script SolsticeTown_EventScript_Girl1_Dialogue {
    applymovement(SolsticeTown_Girl1, moves(face_player))
    msgbox(format("Did you know you can press Start to check your Pokémon summary?"), type=MSGBOX_NPC)
    applymovement(SolsticeTown_Girl1, moves(face_right))
}

script SolsticeTown_EventScript_Boy1_Dialogue {
    applymovement(SolsticeTown_Boy1, moves(face_player))
    msgbox(format("I use the Pokémon Summary screen to check my Pokémon's nature."), type=MSGBOX_NPC)
    applymovement(SolsticeTown_Boy1, moves(face_left))
}

script SolsticeTown_EventScript_Girl2_Dialogue {
    applymovement(SolsticeTown_Girl2, moves(face_player))
    msgbox(format("Solstice Town doesn't have a gym.\n"
        "It's a bit too out of the way for us to have one."), type=MSGBOX_NPC)
}

script SolsticeTown_EventScript_Boy2_Dialogue_SpotA {
    if (!flag(FLAG_DELIVERED_JIRACHI_MEDICINE)) {
        applymovement(SolsticeTown_Boy2, SolsticeTown_ExclamationMark)
        playse(SE_PIN)
        waitse()
        waitmovement(0)
        applymovement(SolsticeTown_Boy2, SolsticeTown_Boy2_RunsToPlayer_SpotA)
        applymovement(LOCALID_PLAYER, moves(face_right))
        msgbox(SolsticeTown_Boy2_Dialogue_PreDelivery, MSGBOX_AUTOCLOSE)
        applymovement(LOCALID_PLAYER, moves(walk_down))
        waitmovement(0)
        applymovement(SolsticeTown_Boy2, SolsticeTown_Boy2_RunsBack_SpotA)
        waitmovement(0)
    }
}

script SolsticeTown_EventScript_Boy2_Dialogue_SpotB {
    if (!flag(FLAG_DELIVERED_JIRACHI_MEDICINE)) {
        applymovement(SolsticeTown_Boy2, SolsticeTown_ExclamationMark)
        playse(SE_PIN)
        waitse()
        waitmovement(0)
        applymovement(SolsticeTown_Boy2, SolsticeTown_Boy2_RunsToPlayer_SpotB)
        applymovement(LOCALID_PLAYER, moves(face_right))
        msgbox(SolsticeTown_Boy2_Dialogue_PreDelivery, MSGBOX_AUTOCLOSE)
        applymovement(LOCALID_PLAYER, moves(walk_down))
        waitmovement(0)
        applymovement(SolsticeTown_Boy2, SolsticeTown_Boy2_RunsBack_SpotB)
        waitmovement(0)
    }
}

script SolsticeTown_EventScript_Boy2_Dialogue_SpotC {
    if (!flag(FLAG_DELIVERED_JIRACHI_MEDICINE)) {
        applymovement(SolsticeTown_Boy2, SolsticeTown_ExclamationMark)
        playse(SE_PIN)
        waitse()
        waitmovement(0)
        applymovement(LOCALID_PLAYER, moves(face_right))
        msgbox(SolsticeTown_Boy2_Dialogue_PreDelivery, MSGBOX_AUTOCLOSE)
        applymovement(LOCALID_PLAYER, moves(walk_down))
        waitmovement(0)
    }
}

script SolsticeTown_EventScript_Boy2_Dialogue {
    if (!flag(FLAG_DELIVERED_JIRACHI_MEDICINE)) {
        applymovement(SolsticeTown_Boy2, moves(face_player))
        msgbox(SolsticeTown_Boy2_Dialogue_PreDelivery, type=MSGBOX_NPC)
    } else {
        applymovement(SolsticeTown_Boy2, moves(face_player))
        msgbox(SolsticeTown_Boy2_Dialogue_PostDelivery, type=MSGBOX_NPC)
    }
}

script SolsticeTown_EventScript_RivalRunsToPlayerAtA {
    if (var(VAR_INTRO_SCENE) == 6) {
        lockall
        clearflag(FLAG_HIDE_RIVAL_IN_SOLSTICE_TOWN)
        addobject(SolsticeTown_Rival)
        playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
        applymovement(LOCALID_PLAYER, SolsticeTown_ExclamationMark)
        playse(SE_PIN)
        waitmovement(0)
        delay(20)
        applymovement(LOCALID_PLAYER, moves(face_down))
        waitmovement(0)
        applymovement(SolsticeTown_Rival, SolsticeTown_Movement_RivalRunsToPlayerAtA)
        waitmovement(0)
        applymovement(LOCALID_PLAYER, moves(face_right))
        waitmovement(0)
        call(SolsticeTown_EventScript_Rival_First_Battle)
        releaseall
    }
}

script SolsticeTown_EventScript_RivalRunsToPlayerAtB {
    if (var(VAR_INTRO_SCENE) == 6) {
        lockall
        clearflag(FLAG_HIDE_RIVAL_IN_SOLSTICE_TOWN)
        addobject(SolsticeTown_Rival)
        playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
        applymovement(LOCALID_PLAYER, SolsticeTown_ExclamationMark)
        playse(SE_PIN)
        waitmovement(0)
        delay(20)
        applymovement(LOCALID_PLAYER, moves(face_down))
        waitmovement(0)
        applymovement(SolsticeTown_Rival, SolsticeTown_Movement_RivalRunsToPlayerAtB)
        waitmovement(0)
        applymovement(LOCALID_PLAYER, moves(face_down))
        waitmovement(0)
        call(SolsticeTown_EventScript_Rival_First_Battle)
        releaseall
    }
}

script SolsticeTown_EventScript_RivalRunsToPlayerAtC {
    if (var(VAR_INTRO_SCENE) == 6) {
        lockall
        clearflag(FLAG_HIDE_RIVAL_IN_SOLSTICE_TOWN)
        addobject(SolsticeTown_Rival)
        playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
        applymovement(LOCALID_PLAYER, SolsticeTown_ExclamationMark)
        playse(SE_PIN)
        waitmovement(0)
        delay(20)
        applymovement(LOCALID_PLAYER, moves(face_down))
        waitmovement(0)
        applymovement(SolsticeTown_Rival, SolsticeTown_Movement_RivalRunsToPlayerAtC)
        waitmovement(0)
        applymovement(LOCALID_PLAYER, moves(face_left))
        waitmovement(0)
        call(SolsticeTown_EventScript_Rival_First_Battle)
        releaseall
    }
}

script SolsticeTown_EventScript_RivalRunsToPlayerAtD {
    if (var(VAR_INTRO_SCENE) == 6) {
        lockall
        clearflag(FLAG_HIDE_RIVAL_IN_SOLSTICE_TOWN)
        addobject(SolsticeTown_Rival)
        playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
        applymovement(LOCALID_PLAYER, SolsticeTown_ExclamationMark)
        playse(SE_PIN)
        waitmovement(0)
        delay(20)
        applymovement(LOCALID_PLAYER, moves(face_down))
        waitmovement(0)
        applymovement(SolsticeTown_Rival, SolsticeTown_Movement_RivalRunsToPlayerAtD)
        waitmovement(0)
        applymovement(LOCALID_PLAYER, moves(face_left))
        waitmovement(0)
        call(SolsticeTown_EventScript_Rival_First_Battle)
        releaseall
    }
}

script SolsticeTown_EventScript_Rival_First_Battle {
    lockall
    checkplayergender()
    if (var(VAR_RESULT) == MALE) {
        call(SolsticeTown_EventScript_RivalSelene)
    }
    else {
        call (SolsticeTown_EventScript_RivalCassian)
    }
    releaseall
}

script SolsticeTown_EventScript_RivalSelene {
    call(RivalName)
    msgbox(SolsticeTown_Rival_Battle_Intro_Dialogue)
    switch (var(VAR_STARTER_MON)) {
        case 0:
            call(SolsticeTown_EventScript_StartSeleneBattleSprigatito)
        case 1:
            call(SolsticeTown_EventScript_StartSeleneBattleSquirtle)
        case 2:
            call(SolsticeTown_EventScript_StartSeleneBattleFennekin)
    }
}

script SolsticeTown_EventScript_RivalCassian {
    call(RivalName)
    msgbox(SolsticeTown_Rival_Battle_Intro_Dialogue)
    switch (var(VAR_STARTER_MON)) {
        case 0:
            call(SolsticeTown_EventScript_StartCassianBattleSprigatito)
        case 1:
            call(SolsticeTown_EventScript_StartCassianBattleSquirtle)
        case 2:
            call(SolsticeTown_EventScript_StartCassianBattleFennekin)
    }
}

script SolsticeTown_EventScript_StartSeleneBattleSprigatito {
    trainerbattle_no_intro(TRAINER_SELENE_SOLSTICE_SPRIGATITO, SolsticeTown_Rival_Battle_Defeat_Dialogue)
    call(SolsticeTown_EventScript_RivalExit)
}

script SolsticeTown_EventScript_StartSeleneBattleFennekin {
    trainerbattle_no_intro(TRAINER_SELENE_SOLSTICE_FENNEKIN, SolsticeTown_Rival_Battle_Defeat_Dialogue)
    call(SolsticeTown_EventScript_RivalExit)
}

script SolsticeTown_EventScript_StartSeleneBattleSquirtle {
    trainerbattle_no_intro(TRAINER_SELENE_SOLSTICE_SQUIRTLE, SolsticeTown_Rival_Battle_Defeat_Dialogue)
    call(SolsticeTown_EventScript_RivalExit)
}

script SolsticeTown_EventScript_StartCassianBattleSprigatito {
    trainerbattle_no_intro(TRAINER_CASSIAN_SOLSTICE_SPRIGATITO, SolsticeTown_Rival_Battle_Defeat_Dialogue)
    call(SolsticeTown_EventScript_RivalExit)
}

script SolsticeTown_EventScript_StartCassianBattleFennekin {
    trainerbattle_no_intro(TRAINER_CASSIAN_SOLSTICE_FENNEKIN, SolsticeTown_Rival_Battle_Defeat_Dialogue)
    call(SolsticeTown_EventScript_RivalExit)
}

script SolsticeTown_EventScript_StartCassianBattleSquirtle {
    trainerbattle_no_intro(TRAINER_CASSIAN_SOLSTICE_SQUIRTLE, SolsticeTown_Rival_Battle_Defeat_Dialogue)
    call(SolsticeTown_EventScript_RivalExit)
}

script SolsticeTown_EventScript_RivalExit {
    setvar(VAR_INTRO_SCENE, 7)
    msgbox(SolsticeTown_Rival_After_Battle_Dialogue, MSGBOX_AUTOCLOSE)
    applymovement(SolsticeTown_Rival, SolsticeTown_Movement_RivalExit)
    waitmovement(0)
    fadedefaultbgm()
    setflag(FLAG_HIDE_RIVAL_IN_SOLSTICE_TOWN)
    removeobject(SolsticeTown_Rival)
    releaseall
}

text SolsticeTown_Boy2_Dialogue_PreDelivery {
    format("WHOA there, superstar! The road ahead's closed.\l"
    "Not by law or anything, I'm just standing here real confidently.\l"
    "You look like you've got something important to do back home anyway.")
}

text SolsticeTown_Boy2_Dialogue_PostDelivery {
    format("Ahh, the hero returns!\n"
        "I'd move out of the way dramatically, but my back's acting up.\l"
        "Just pretend I did a cool spin or something.")
}

text SolsticeTown_Rival_Battle_Intro_Dialogue {
    format("{PLAYER}, wait!\p"
        "So you're leaving on an adventure too, huh?\l"
        "Now that I have a Pokémon, I also want to go out and explore the world!\l"
        "We'll take the Pokémon League challenge together and see who comes out on top!\l"
        "Before we leave, how about a battle?\l"
        "I'll give you a taste of what it means to compete with me!")
}

text SolsticeTown_Rival_Battle_Defeat_Dialogue {
    format("What...? Maybe I picked the wrong Pokémon...")
}

text SolsticeTown_Rival_After_Battle_Dialogue {
    format("Humpft... You only won because you had a head start.\l"
        "You won't be so lucky next time, though!\l"
        "Me and my Pokémon will train and get much stronger, you'll see!")
}

movement SolsticeTown_ExclamationMark {
    emote_exclamation_mark
}

movement SolsticeTown_Boy2_RunsToPlayer_SpotA {
    walk_fast_left * 2
}

movement SolsticeTown_Boy2_RunsBack_SpotA {
    walk_right * 2
    face_left
}

movement SolsticeTown_Boy2_RunsToPlayer_SpotB {
    walk_fast_left
}

movement SolsticeTown_Boy2_RunsBack_SpotB {
    walk_right
    face_left
}

movement SolsticeTown_Movement_RivalRunsToPlayerAtA {
    walk_fast_up * 9
    face_left
}

movement SolsticeTown_Movement_RivalRunsToPlayerAtB {
    walk_fast_up * 8
    face_up
}

movement SolsticeTown_Movement_RivalRunsToPlayerAtC {
    walk_fast_up * 9
    face_right
}

movement SolsticeTown_Movement_RivalRunsToPlayerAtD {
    walk_fast_up * 9
    walk_fast_right
    face_right
}

movement SolsticeTown_Movement_RivalExit {
    walk_fast_down * 8
}