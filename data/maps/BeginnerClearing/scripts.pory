mapscripts BeginnerClearing_MapScripts {
    
}

movement BeginnerClearing_ExclamationMark {
    emote_exclamation_mark
}

movement BeginnerClearing_QuestionMark {
    emote_question_mark
}

movement BeginnerClearing_WalkUpToJirachiFromA {
    walk_fast_right * 5
    walk_fast_up * 2
    face_right
}

movement BeginnerClearing_WalkUpToJirachiFromB {
    walk_fast_right * 5
    walk_fast_up * 3
    face_right
}

movement BeginnerClearing_MyraRunsInA {
    walk_fast_right * 3
}

movement BeginnerClearing_MyraRunsInB {
    walk_fast_right * 6
    walk_fast_up * 1
}

movement BeginnerClearing_GruntAWalksIn {
    walk_fast_right * 4
}

movement BeginnerClearing_GruntBWalksIn {
    walk_fast_right * 5
}

movement BeginnerClearing_PlayerRunsInFrontOfGrunts {
    walk_fast_down * 2
    walk_fast_left
    face_left
}

movement BeginnerClearing_ProfMyraWalksToPlayer {
    walk_down * 2
    walk_left * 2
}

movement BeginnerClearing_GruntARunsToPlayer {
    walk_fast_right * 4
}

movement BeginnerClearing_GruntBRunsToProfMyra {
    walk_fast_right * 3
}

movement BeginnerClearing_WalkToGruntA_LeftSideOfBag {
    walk_fast_down
    walk_fast_left
}

movement BeginnerClearing_WalkToGruntA_TopSideOfBag {
    walk_fast_right
    walk_fast_down * 2
    walk_fast_left * 3
}

movement BeginnerClearing_WalkToGruntA_RightSideOfBag {
    walk_fast_down
    walk_fast_left * 3
}

movement BeginnerClearing_WalkToGruntA_BottomSideOfBag {
    walk_fast_left * 2
}

movement BeginnerClearing_PlayerWalksIn_LookForJirachiFromA {
    delay_16
    walk_right * 7
    walk_up
    delay_16
    delay_16
    face_left
    delay_16
    delay_16
    delay_16
    face_right
    delay_16
    delay_16
    delay_16
    face_up
}

movement BeginnerClearing_PlayerWalksIn_LookForJirachiFromB {
    delay_16
    walk_right * 7
    walk_up * 2
    delay_16
    delay_16
    face_left
    delay_16
    delay_16
    delay_16
    face_right
    delay_16
    delay_16
    delay_16
    face_up
}

text ProfMyraName {
    "Prof. Myra"
}

text JirachiName {
    "Jirachi"
}

text GruntAName {
    "Stranger 1"
}

text GruntBName {
    "Stranger 2"
}

script BeginnerClearing_EventScript_WalkFromA {
    lockall
    if (!flag(FLAG_CAN_LEAVE_BEGINNER_CLEARING)) {
        playmoncry(SPECIES_JIRACHI, CRY_MODE_FAINT)
        waitmoncry()
        applymovement(LOCALID_PLAYER, BeginnerClearing_ExclamationMark)
        playse(SE_PIN)
        waitmovement(0)
        delay(50)
        applymovement(LOCALID_PLAYER, BeginnerClearing_WalkUpToJirachiFromA)
        waitmovement(0)
        call(BeginnerClearing_EventScript_JirachiIntro)
    }
    releaseall
}

script BeginnerClearing_EventScript_WalkFromB {
    lockall
    if (!flag(FLAG_CAN_LEAVE_BEGINNER_CLEARING)) {
        playmoncry(SPECIES_JIRACHI, CRY_MODE_FAINT)
        waitmoncry()
        applymovement(LOCALID_PLAYER, BeginnerClearing_ExclamationMark)
        playse(SE_PIN)
        waitmovement(0)
        delay(50)
        applymovement(LOCALID_PLAYER, BeginnerClearing_WalkUpToJirachiFromB)
        waitmovement(0)
        call(BeginnerClearing_EventScript_JirachiIntro)
    }
    releaseall
}

script BeginnerClearing_EventScript_ProfMyra {
    speakername(ProfMyraName)
    msgbox("I will lend you one of my Pokémon so\nwe can defend this Jirachi together.\l"
        "Please grab a Pokéball from my bag.", MSGBOX_NPC)
}

script BeginnerClearing_EventScript_GruntA {
    speakername(GruntAName)
    msgbox(format("Fool! Hand Jirachi over and don't interfere!"), MSGBOX_NPC)
}

script BeginnerClearing_EventScript_GruntB {
    speakername(GruntBName)
    msgbox(format("Don't meddle where you aren't supposed to!"), MSGBOX_NPC)
}

script BeginnerClearing_EventScript_JirachiNPC {
    playmoncry(SPECIES_JIRACHI, CRY_MODE_FAINT)
    waitmoncry()
}

script BeginnerClearing_EventScript_JirachiIntro {
    lockall
    clearflag(FLAG_HIDE_CLEARING_NPCS)
    speakername(JirachiName)
    msgbox(format("{COLOR RED}Help… they're coming after me…"))
    playse(SE_SELECT)
    delay(10)
    applymovement(LOCALID_PLAYER, BeginnerClearing_QuestionMark)
    playse(SE_PIN)
    waitmovement(0)
    delay(50)
    msgbox(format("{COLOR RED}They took my friend… Help…"), MSGBOX_AUTOCLOSE)
    playse(SE_SELECT)
    addobject(ProfMyra)
    applymovement(ProfMyra, BeginnerClearing_MyraRunsInA)
    waitmovement(0)
    applymovement(ProfMyra, BeginnerClearing_ExclamationMark)
    playse(SE_PIN)
    waitmovement(0)
    delay(50)
    applymovement(ProfMyra, BeginnerClearing_MyraRunsInB)
    waitmovement(0)
    addobject(MyrasBag)
    speakername(ProfMyraName)
    msgbox(format("Is that a Jirachi? Oh, it's hurt!"))
    playse(SE_SELECT)
    msgbox(format("Quick, I have a Potion!"))
    playse(SE_USE_ITEM)
    waitse()
    msgbox(format("It should be okay for now, but it needs rest.\p"
        "The poor thing's definitely in bad shape.\p"
        "I wonder what happened…"))
    playse(SE_SELECT)
    applymovement(ProfMyra, moves(face_left))
    waitmovement(0)
    msgbox(format("What's your name?"))
    msgbox(format("…"))
    msgbox(format(
        "{PLAYER}, is it?\p"
        "We are lucky you found it so quickly.\p"
        "We should take it somewhere to recover.\p"
        "…\p"
        "You said Jirachi spoke to you? And that it said it's being chased?"), MSGBOX_AUTOCLOSE)
    playse(SE_SELECT)
    addobject(GruntA)
    addobject(GruntB)
    applymovement(GruntA, BeginnerClearing_GruntAWalksIn)
    applymovement(GruntB, BeginnerClearing_GruntBWalksIn)
    waitmovement(0)
    playbgm(MUS_RG_ROCKET_HIDEOUT, FALSE)
    delay(40)
    applymovement(GruntA, BeginnerClearing_ExclamationMark)
    applymovement(GruntB, BeginnerClearing_ExclamationMark)
    playse(SE_PIN)
    waitmovement(0)
    delay(50)
    applymovement(LOCALID_PLAYER, moves(face_left))
    applymovement(ProfMyra, moves(face_left))
    special(SpawnCameraObject)
    applymovement(LOCALID_CAMERA, moves(walk_diag_southwest*2))
    waitmovement(0)
    speakername(GruntAName)
    msgbox(format("Here it is! Grab it!"))
    playse(SE_SELECT)
    speakername(GruntBName)
    msgbox(format("You! Hand over that Jirachi!"), MSGBOX_AUTOCLOSE)
    playse(SE_SELECT)
    delay(10)
    applymovement(LOCALID_PLAYER, BeginnerClearing_ExclamationMark)
    applymovement(ProfMyra, BeginnerClearing_ExclamationMark)
    playse(SE_PIN)
    waitmovement(0)
    delay(50)
    applymovement(LOCALID_PLAYER, BeginnerClearing_PlayerRunsInFrontOfGrunts)
    applymovement(LOCALID_CAMERA, moves(walk_right))
    waitmovement(0)
    delay(30)
    special(RemoveCameraObject)
    speakername(GruntAName)
    msgbox(format("Huh? Who are you?"))
    playse(SE_SELECT)
    speakername(GruntBName)
    msgbox(format("You think a dweeb like you can get in our way?"))
    playse(SE_SELECT)
    speakername(ProfMyraName)
    msgbox(format("And who are you? What do you want with this Jirachi?"))
    playse(SE_SELECT)
    speakername(GruntBName)
    msgbox(format("That's none of your business, lady!"))
    playse(SE_SELECT)
    speakername(GruntAName)
    msgbox(format("Just hand over the Jirachi and no one gets hurt!"), MSGBOX_AUTOCLOSE)
    playse(SE_SELECT)
    applymovement(ProfMyra, BeginnerClearing_ProfMyraWalksToPlayer)
    waitmovement(0)
    speakername(ProfMyraName)
    msgbox(format("{PLAYER}, do you have any Pokémon?\p"
        "…\p"
        "Hmmm, I see. I think this situation calls for special circumstances.\p"
        "I will lend you one of my Pokémon so we can defend this Jirachi together.\p"
        "Please grab a Pokéball from my bag."))
    setobjectxyperm(GruntA, 7, 13)
    setobjectxyperm(GruntB, 7, 14)
    setobjectxyperm(ProfMyra, 11, 14)
    releaseall
}

script BeginnerClearing_EventScript_MyrasBag {
    lockall
    fadescreenswapbuffers(FADE_TO_BLACK)
    setflag(FLAG_SYS_POKEMON_GET)
    special(ChooseStarter)
    waitstate()
    call(BeginnerClearing_EventScript_GruntABattle)
    releaseall
}

script BeginnerClearing_EventScript_GruntABattle {
    lockall
    applymovement(GruntA, BeginnerClearing_GruntARunsToPlayer)
    applymovement(GruntB, BeginnerClearing_GruntBRunsToProfMyra)
    waitmovement(0)
    switch (var(VAR_FACING)) {
        case DIR_EAST: // facing east, player is left of bag
            applymovement(LOCALID_PLAYER, BeginnerClearing_WalkToGruntA_LeftSideOfBag)
        case DIR_SOUTH: // facing south, player is above bag
            applymovement(LOCALID_PLAYER, BeginnerClearing_WalkToGruntA_TopSideOfBag)
        case DIR_WEST: // facing west, player is right of bag
            applymovement(LOCALID_PLAYER, BeginnerClearing_WalkToGruntA_RightSideOfBag)
        case DIR_NORTH: // facing north, player is below bag
            applymovement(LOCALID_PLAYER, BeginnerClearing_WalkToGruntA_BottomSideOfBag)
    }
    waitmovement(LOCALID_PLAYER)
    applymovement(ProfMyra, moves(face_left))
    trainerbattle_single(TRAINER_GRUNT_MAGMA_HIDEOUT_14, "You're going down, kid!", "Impossible…", BeginnerClearing_EventScript_AfterBattle)
    releaseall
}

script BeginnerClearing_EventScript_AfterBattle {
    lockall
    speakername(GruntAName)
    msgbox(format("How is this possible!?\p"
        "You little brat… You'll regret this!"), MSGBOX_AUTOCLOSE)
    playse(SE_SELECT)
    applymovement(GruntA, moves(walk_fast_left * 10))
    waitmovement(0)
    speakername(GruntBName)
    msgbox(format("You don't mess with Team Eclipse and get away with it!"), MSGBOX_AUTOCLOSE)
    playse(SE_SELECT)
    applymovement(GruntB, moves(walk_fast_left * 10))
    waitmovement(0)
    applymovement(ProfMyra, moves(
        walk_right
        face_up))
    applymovement(LOCALID_PLAYER, moves(face_down))
    waitmovement(0)
    speakername(ProfMyraName)
    msgbox(format("{PLAYER}, you were amazing!\p"
        "I'm so glad you were here.\p"
        "Now let's take this Jirachi back to my lab so it can rest."), MSGBOX_AUTOCLOSE)
    playse(SE_SELECT)
    setflag(FLAG_CAN_LEAVE_BEGINNER_CLEARING)
    setflag(FLAG_HIDE_CLEARING_NPCS)
    setflag(FLAG_HIDE_CLEARING_JIRACHI)
    special(HealPlayerParty)
    warpsilent(MAP_LUMEN_VILLAGE_PROFESSOR_MYRAS_LAB, 6, 12)
    releaseall
}

script BeginnerClearing_EventScript_LookForJirachiFromA {
    if (var(VAR_INTRO_SCENE) == 5) {
        lockall
        setvar(VAR_INTRO_SCENE, 6)
        hidefollower()
        applymovement(LOCALID_PLAYER, BeginnerClearing_PlayerWalksIn_LookForJirachiFromA)
        waitmovement(0)
        call(BeginnerClearing_EventScript_JirachiPlayerFirstDialogue)
        releaseall
    }
}

script BeginnerClearing_EventScript_LookForJirachiFromB {
    if (var(VAR_INTRO_SCENE) == 5) {
        lockall
        setvar(VAR_INTRO_SCENE, 6)
        hidefollower()
        applymovement(LOCALID_PLAYER, BeginnerClearing_PlayerWalksIn_LookForJirachiFromB)
        waitmovement(0)
        call(BeginnerClearing_EventScript_JirachiPlayerFirstDialogue)
        releaseall
    }
}

script BeginnerClearing_EventScript_JirachiPlayerFirstDialogue {
    lockall
    applymovement(LOCALID_PLAYER, BeginnerClearing_ExclamationMark)
    playse(SE_PIN)
    waitmovement(0)
    clearflag(FLAG_HIDE_CLEARING_JIRACHI2)
    addobject(jirachi_intro_2)
    applymovement(jirachi_intro_2, moves(walk_slow_down * 8))
    waitmovement(0)
    speakername(JirachiName)
    msgbox(format("{COLOR RED}…you came.\p"
        "I wasn't sure you'd hear me, but… you did.\p"
        "Those people, they tried to take me. They hurt me.\p"
        "But I'm not the only one.\p"
        "There was another. A friend. Like me.\p"
        "They took him. Locked him away… and twisted his voice.\p"
        "His wishes… used for control. For silence.\p"
        "…I'm still too weak. But I can feel it… minds cracking. Dreams bending.\p"
        "I need help…  I need to save my friend. And I need to stop them.\p"
        "Will you help me stop them?\p"
        "Will you help me bring my friend back?\p"
        "…\p"
        "Thank you, {PLAYER}.\p"
        "He's being held in a place far from here.\p"
        "A place of towers and metal… minds shine brightly there, but not freely.\p"
        "Zenith City, I believe is the name.\p"
        "They use him there. To twist thoughts. To turn strong hearts into hollow voices.\p"
        "One by one, they fall under the silence.\p"
        "Leaders. Champions. Protectors.\p"
        "…If we don't act soon, no one will remain untouched.\p"
        "I can't go with you… not yet.\p"
        "But I'll guide you when I can.\p"
        "There are other minds I can still reach. Free them, and we'll be closer to saving him, and closer to the truth.\p"
        "Closer to stopping the silence.\p"
        "Thank you for your help, {PLAYER}.\p"
        "I'll find you again when I can."))
        fadescreenswapbuffers(FADE_TO_WHITE)
        setflag(FLAG_HIDE_CLEARING_JIRACHI2)
        setflag(FLAG_HIDE_MYRA_IN_LAB)
        removeobject(jirachi_intro_2)
        fadescreenswapbuffers(FADE_FROM_WHITE)
        completequest(QUEST_1_MEDS_FOR_JIRACHI)
        startquest(QUEST_2_STARBOUND_OATH)
        subquestmenu(QUEST_MENU_SET_ACTIVE, QUEST_2_STARBOUND_OATH, SUB_QUEST_GO_TO_ZENITH_CITY)
        openquestmenu()
        releaseall
}