mapscripts Astridale_Mindscape_MapScripts {
    MAP_SCRIPT_ON_RESUME {
        setflag(FLAG_NO_CATCHING)
        setflag(FLAG_FORCE_NO_SHINY)
        setrespawn(HEAL_LOCATION_ASTRIDALE_MINDSCAPE)
        if (var(VAR_ASTRIDALE_STATE) == 6) {
            call(Astridale_Mindscape_Set_Key_Metatiles)
            setobjectxyperm(Astridale_Mindscape_Jirachi, 20, 75)
        }
        elif (var(VAR_ASTRIDALE_STATE) == 7) {
            call(Astridale_Mindscape_Set_Key_Metatiles)
            call(Astridale_Mindscape_Set_LockBox_Metatiles)
            setobjectxyperm(Astridale_Mindscape_Jirachi, 20, 75)
        }
    }

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ASTRIDALE_STATE, 4 {
            setvar(VAR_ASTRIDALE_STATE, 5)
            call(Astridale_Mindscape_EventScript_Intro_Dialogue)
            setobjectxyperm(Astridale_Mindscape_Jirachi, 20, 75)
            end
        }
    ]
}

script Astridale_Mindscape_EventScript_Intro_Dialogue {
    lockall
    applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark))
    playse(SE_PIN)
    waitse()
    waitmovement(LOCALID_PLAYER)
    applymovement(LOCALID_PLAYER, moves(face_up))
    waitmovement(LOCALID_PLAYER)
    applymovement(Astridale_Mindscape_Jirachi, moves(walk_slow_down * 6))
    waitmovement(Astridale_Mindscape_Jirachi)
    applymovement(Astridale_Mindscape_Jirachi, moves(face_left))
    waitmovement(Astridale_Mindscape_Jirachi)
    applymovement(LOCALID_PLAYER, moves(face_right))
    waitmovement(LOCALID_PLAYER)
    setspeaker(JirachiName)
    msgbox(format("{COLOR RED}{PLAYER}.\p"
        "This is the realm of dreams.\p"
        "You are in Viviane's mindscape now.\p"
        "I was able to pull you in once you showed her the Clefairy doll.\p"
        "You did well, {PLAYER}.\p"
        "Now we should be able to look for her here and see if she's under mind control.\p"
        "I should warn you, mindscapes can be very unpredictable.\p"
        "They reflect a person's innermost self, and there might be challenges you face before you can find Viviane.\p"
        "Every person's mindscape is different, so I can't tell you what you'll find here.\p"
        "But just keep in mind that nothing you encounter here is real - people, Pokémon, items.\p"
        "Whatever Pokémon you find here, you won't be able to catch, since they don't exist in the real world - only in Viviane's consciousness.\p"
        "Likewise, anyone you encounter, besides Viviane herself, is only a figment of her mind.\p"
        "I'll stay here to support you, so please go and find Viviane."), MSGBOX_AUTOCLOSE)
    releaseall
}

script Astridale_Mindscape_EventScript_Jirachi_Dialogue {
    applymovement(Astridale_Mindscape_Jirachi, moves(face_player))
    setspeaker(JirachiName)
    msgbox(format("{COLOR RED}{PLAYER}, you should rest while you can."), MSGBOX_NPC)
    call(Common_EventScript_OutOfCenterPartyHeal)
    setspeaker(JirachiName)
    msgbox(format("{COLOR RED}I'm counting on you, {PLAYER}."), MSGBOX_AUTOCLOSE)
}

script Astridale_Mindscape_EventScript_Munna_PC {
    applymovement(Astridale_Mindscape_Munna, moves(face_player))
    msgbox(format("Muuuunn…"), MSGBOX_NPC)
    special(ShowPokemonStorageSystemPC)
    waitstate()
    closemessage()
}

script Astridale_Mindscape_EventScript_LockBox_Trigger {
	if (var(VAR_ASTRIDALE_STATE) < 7) {
		lockall
		msgbox(format("A strange force doesn't allow you through…"), MSGBOX_AUTOCLOSE)
        playse(SE_SELECT)
		applymovement(LOCALID_PLAYER, moves(walk_left))
		waitmovement(0)
		releaseall
	}
}

script Astridale_Mindscape_EventScript_Key {
    lockall
    msgbox(format("You found a key!\p"
    "It looks like it might open a lock box.\p"
    "Would you like to pick it up?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO) {
        msgbox(format("You decided not to pick up the key."), MSGBOX_AUTOCLOSE)
    }
    else {
        setvar(VAR_ASTRIDALE_STATE, 6)
        setflag(FLAG_HIDE_ASTRIDALE_MINDSCAPE_INV_BRENDAN_KEY)
        removeobject(Astridale_Mindscape_Invisible_Brendan_Key)
        playse(SE_ITEMFINDER)
        waitse()
        msgbox(format("You picked up the key."), MSGBOX_AUTOCLOSE)
        call(Astridale_Mindscape_Set_Key_Metatiles)
    }
    releaseall
}

script Astridale_Mindscape_Set_Key_Metatiles {
    setmetatile(53, 18, METATILE_AstridaleMindscape_astridale_mindscape_ground, FALSE)
    setmetatile(51, 19, METATILE_AstridaleMindscape_astridale_mindscape_maze_middle_edge, TRUE)
    setmetatile(51, 20, METATILE_AstridaleMindscape_astridale_mindscape_maze_bottom_edge, TRUE)
    setmetatile(52, 19, METATILE_AstridaleMindscape_astridale_mindscape_ground, FALSE)
    setmetatile(52, 20, METATILE_AstridaleMindscape_astridale_mindscape_ground, FALSE)
    setmetatile(53, 19, METATILE_AstridaleMindscape_astridale_mindscape_ground, FALSE)
    setmetatile(53, 20, METATILE_AstridaleMindscape_astridale_mindscape_ground, FALSE)
    setmetatile(54, 19, METATILE_AstridaleMindscape_astridale_mindscape_maze_middle_edge, TRUE)
    setmetatile(54, 20, METATILE_AstridaleMindscape_astridale_mindscape_maze_bottom_edge, TRUE)
    special(DrawWholeMapView)
}

script Astridale_Mindscape_EventScript_LockBox {
    if (var(VAR_ASTRIDALE_STATE) < 6) {
        lockall
        msgbox(format("The lock box is locked.\p"
            "It looks like it might be opened with a key."), MSGBOX_AUTOCLOSE)
        releaseall
    }
    else {
        lockall
        setvar(VAR_ASTRIDALE_STATE, 7)
        msgbox(format("You opened the lock box with the key you found."), MSGBOX_AUTOCLOSE)
        setflag(FLAG_HIDE_ASTRIDALE_MINDSCAPE_INV_BRENDAN_LOCKBOX)
        removeobject(Astridale_Mindscape_Invisible_Brendan_LockBox)
        call(Astridale_Mindscape_Set_LockBox_Metatiles)
        releaseall
    }
}

script Astridale_Mindscape_Set_LockBox_Metatiles {
    setmetatile(39, 61, METATILE_AstridaleMindscape_astridale_mindscape_ground, FALSE)
    setmetatile(39, 62, METATILE_AstridaleMindscape_astridale_mindscape_ground, FALSE)
    special(DrawWholeMapView)
}

script Astridale_Mindscape_EventScript_Viviane {
    lockall
    setspeaker(VivianeName)
    trainerbattle_single(TRAINER_VIVIANE_1, Astridale_Mindscape_Text_VivianeIntro, Astridale_Mindscape_Text_VivianeDefeat, Astridale_Mindscape_EventScript_VivianeDefeated, NO_MUSIC)
    releaseall
}

text Astridale_Mindscape_Text_VivianeIntro {
    format("…\p"
        "I must destroy the construction in Astridale…\p"
        "You wouldn't understand…\p"
        "I must… I must…\p"
        "I…")
}

text Astridale_Mindscape_Text_VivianeDefeat {
    format("No… I can't lose…")
}

script Astridale_Mindscape_EventScript_VivianeDefeated {
    setvar(VAR_ASTRIDALE_STATE, 8)
    applymovement(Astridale_Mindscape_Viviane, moves(face_player))
    setspeaker(VivianeName)
    msgbox(format("…I… I can't remember…\p"
        "What was I doing?\p"
        "I feel so tired…\p"
        "I think I need to rest for a while…"), MSGBOX_AUTOCLOSE)
    warpwhitefade(MAP_ASTRIDALE_GYM, 7, 3)
}

script Astridale_Mindscape_EventScript_Beauty_Melissa {
    trainerbattle_single(TRAINER_MELISSA, Astridale_Mindscape_Beauty_Melissa_Intro_Text, Astridale_Mindscape_Beauty_Melissa_Lose_Text)
    msgbox(Astridale_Mindscape_Beauty_Melissa_Dialogue, MSGBOX_NPC)
}

script Astridale_Mindscape_EventScript_Lass_Tiana {
    trainerbattle_single(TRAINER_TIANA, Astridale_Mindscape_Lass_Tiana_Intro_Text, Astridale_Mindscape_Lass_Tiana_Lose_Text)
    msgbox(Astridale_Mindscape_Lass_Tiana_Dialogue, MSGBOX_NPC)
}

script Astridale_Mindscape_EventScript_Lady_Cindy {
    trainerbattle_single(TRAINER_CINDY_1, Astridale_Mindscape_Lady_Cindy_Intro_Text, Astridale_Mindscape_Lady_Cindy_Lose_Text)
    msgbox(Astridale_Mindscape_Lady_Cindy_Dialogue, MSGBOX_NPC)
}

script Astridale_Mindscape_EventScript_AromaLady_Daisy {
    trainerbattle_single(TRAINER_DAISY, Astridale_Mindscape_AromaLady_Daisy_Intro_Text, Astridale_Mindscape_AromaLady_Daisy_Lose_Text)
    msgbox(Astridale_Mindscape_AromaLady_Daisy_Dialogue, MSGBOX_NPC)
}

text Astridale_Mindscape_Beauty_Melissa_Intro_Text {
    format("This place feels like a dream, doesn't it?")
}

text Astridale_Mindscape_Beauty_Melissa_Lose_Text {
    format("You sparkle brighter than I expected…")
}

text Astridale_Mindscape_Beauty_Melissa_Dialogue {
    format("Lady Viviane says every challenger adds a new shimmer to her world. I think yours was a whole constellation.")
}

text Astridale_Mindscape_Lass_Tiana_Intro_Text {
    format("I'm gonna be just like Miss Viviane one day! Wanna see what I've learned?")
}

text Astridale_Mindscape_Lass_Tiana_Lose_Text {
    format("W-wait, I wasn't ready! … Okay, maybe I was.")
}

text Astridale_Mindscape_Lass_Tiana_Dialogue {
    format("Viviane says losing helps you grow. So I'm gonna grow like a Sunkern in the sun!")
}

text Astridale_Mindscape_Lady_Cindy_Intro_Text {
    format("The battlefield is our stage. Let's make this a dazzling duet!")
}

text Astridale_Mindscape_Lady_Cindy_Lose_Text {
    format("Oh! That finale… was magnificent!")
}

text Astridale_Mindscape_Lady_Cindy_Dialogue {
    format("Viviane taught me that grace and strength are not opposites. They're partners.")
}

text Astridale_Mindscape_AromaLady_Daisy_Intro_Text {
    format("I made cupcakes for the Pokémon… but you? You'll get a fairy-type smackdown instead!")
}

text Astridale_Mindscape_AromaLady_Daisy_Lose_Text {
    format("Oh dear! I dropped my whisk AND the match!")
}

text Astridale_Mindscape_AromaLady_Daisy_Dialogue {
    format("Viviane has such a sweet tooth for kindness… and battle strategy!")
}