mapscripts AstridaleCity_House2_MapScripts {

}

script Astridale_House2_EventScript_Girl_Dialogue {
    lockall
    applymovement(Astridale_House2_Girl, moves(face_player))
    if (flag(FLAG_ASTRIDALE_NPC_TRADE_COMPLETED)) {
        msgbox(format("How's my Tink doing?\n"
            "I hope you've been taking good care of it!"), MSGBOX_AUTOCLOSE)
    }
    else {
        setvar(VAR_0x8008, INGAME_TRADE_TINKATINK)
        copyvar(VAR_0x8004, VAR_0x8008)
        specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
        copyvar(VAR_0x8009, VAR_RESULT)
        msgbox(format("Hi! I'm a big fan of Bulbasaur!\p"
            "I hear they live in Whispering Grove, but I'm too young to go that far.\p"
            "Would you trade me a {STR_VAR_1} for my {STR_VAR_2}?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO) {
            msgbox(format("Oh, if you don't want to, that's ok."), MSGBOX_DEFAULT)
        }
        else {
            special(ChoosePartyMon)
            waitstate()
            copyvar(VAR_0x800A, VAR_0x8004)
            if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
                msgbox(format("Oh, if you don't want to, that's ok."), MSGBOX_DEFAULT)
            }
            else {
                copyvar(VAR_0x8005, VAR_0x800A)
                specialvar(VAR_RESULT, GetTradeSpecies)
                copyvar(VAR_0x800B, VAR_RESULT)
                if (var(VAR_RESULT) != VAR_0x8009) {
                    msgbox(format("Huh? That doesn't look anything like a Bulbasaur to me."), MSGBOX_DEFAULT)
                }
                else {
                    copyvar(VAR_0x8004, VAR_0x8008)
                    copyvar(VAR_0x8005, VAR_0x800A)
                    special(CreateInGameTradePokemon)
                    special(DoInGameTradeScene)
                    waitstate()
                    msgbox(format("Please be good to my {STR_VAR_1}."), MSGBOX_DEFAULT)
                    SETFLAG(FLAG_ASTRIDALE_NPC_TRADE_COMPLETED)
                }
            }
        }
    }
    releaseall
}

script Astridale_House2_EventScript_Man_Dialogue {
    applymovement(Astridale_House2_Man, moves(face_player))
    msgbox(format("The power of technology nowadays is amazing.\p"
        "I'm sure the construction will bring even more of it to Astridale.\p"
        "I just hope it doesn't change things too much."), MSGBOX_NPC)
}