mapscripts LumenVillage_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        1, 1 {
            turnobject(LOCALID_PLAYER, DIR_EAST)
        }
    ]

    MAP_SCRIPT_ON_TRANSITION {
        call(Common_EventScript_SetupRivalGfxId)
        if (var(VAR_INTRO_SCENE) < 1) {
            playbgm(MUS_NONE, 1)
        }
    }

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_INTRO_SCENE, 1 {
            setvar(VAR_INTRO_SCENE, 2)
            addobject(LumenVillage_Rival_Intro)
            applymovement(LumenVillage_Rival_Intro, moves(emote_exclamation_mark))
            playse(SE_PIN)
            waitmovement(0)
            playbgm(MUS_RG_ENCOUNTER_RIVAL, FALSE)
            delay(50)
            applymovement(LumenVillage_Rival_Intro, moves(walk_fast_left *3
                                                        walk_fast_up * 3))
            waitmovement(0)
            call(RivalName)
            msgbox(format("Hey, {PLAYER}! What are you doing out here?\p"
                "WHOA - hold up! You've got a Pokémon?!\p"
                "Wait a second… you SERIOUSLY already got a Pokémon??\p"
                "I can't believe this! After all these years training together, and you didn't even tell me??\p"
                "Tch… classic {PLAYER}. Always one step ahead, huh?\p"
                "Well, guess what - I'm not letting you leave me in the dust!\p"
                "I'll get my own Pokémon too, you'll see!"), MSGBOX_AUTOCLOSE)
            applymovement(LumenVillage_Rival_Intro, moves(walk_fast_down * 8))
            waitmovement(0)
            removeobject(LumenVillage_Rival_Intro)
            fadedefaultbgm()
            end
        }
    ]
}

script LumenVillage_EventScript_GoBack_LeftTrigger {
	if (!flag(FLAG_CAN_LEAVE_BEGINNER_CLEARING)) {
		lockall
		msgbox(format("You can't ignore the cries of a hurt Pokémon!"))
        playse(SE_SELECT)
		applymovement(LOCALID_PLAYER, moves(walk_right))
		waitmovement(0)
		releaseall
	}
}

script LumenVillage_EventScript_GoBack_UpTrigger {
	if (!flag(FLAG_CAN_LEAVE_BEGINNER_CLEARING)) {
		lockall
		msgbox(format("You can't ignore the cries of a hurt Pokémon!"))
        playse(SE_SELECT)
		applymovement(LOCALID_PLAYER, moves(walk_down))
		waitmovement(0)
		releaseall
	}
}

script LumenVillage_EventScript_SetIntroFlags {
    checkplayergender()
    if (var(VAR_RESULT) == MALE) {
        call(LumenVillage_EventScript_SetIntroFlags_Male)
    } else {
        call(LumenVillage_EventScript_SetIntroFlags_Female)
    }
}

script LumenVillage_EventScript_SetIntroFlags_Male {
    setrespawn(HEAL_LOCATION_LUMEN_VILLAGE)
    clearflag(FLAG_HIDE_LUMEN_VILLAGE_CASSIANS_HOUSE_MOM)
    clearflag(FLAG_HIDE_LUMEN_VILLAGE_SELENES_HOUSE_RIVAL_MOM)
    addobject(Player_Mom_1F)
    addobject(Rival_Mom_1F)
    releaseall
}

script LumenVillage_EventScript_SetIntroFlags_Female {
    setrespawn(HEAL_LOCATION_LUMEN_VILLAGE)
    clearflag(FLAG_HIDE_LUMEN_VILLAGE_SELENES_HOUSE_MOM)
    clearflag(FLAG_HIDE_LUMEN_VILLAGE_CASSIANS_HOUSE_RIVAL_MOM)
    addobject(Player_Mom_1F)
    addobject(Rival_Mom_1F)
    releaseall
}

script LumenVillage_EventScript_ProfLabSign {
    lockall
    msgbox(format("Prof. Myra's Lab"))
    releaseall
}

script LumenVillage_EventScript_SelenesHouseSign {
    lockall
    checkplayergender()
    if (var(VAR_RESULT) == FEMALE) {
        msgbox(LumenVillage_Text_PlayersHouse_Sign)
    } else {
        msgbox(format("Selene's House"))
    }
    releaseall
}

script LumenVillage_EventScript_CassiansHouseSign {
    lockall
    checkplayergender()
    if (var(VAR_RESULT) == MALE) {
        msgbox(LumenVillage_Text_PlayersHouse_Sign)
    } else {
        msgbox(format("Cassian's House"))
    }
    releaseall
}

script LumenVillage_EventScript_VillageSign {
    lockall
    msgbox(format("Lumen Village\n"
        "“A quiet place beneath the stars”"))
    releaseall
}

script LumenVillage_FatMan_Dialogue {
    applymovement(LumenVillage_FatMan, moves(face_player))
    msgbox(format("Technology is amazing!\p"
        "You can now use satellites to chart the stars in Astrea!"), type=MSGBOX_NPC)
}

script LumenVillage_OldLady_Dialogue {
    applymovement(LumenVillage_OldLady, moves(face_player))
    msgbox(format("Lumen Village must be the safest place in Astrea, but I swear I hear rustling in the night lately…"), type=MSGBOX_NPC)
}

script LumenVillage_LittleGirl_Dialogue {
    applymovement(LumenVillage_LittleGirl, moves(face_player))
    msgbox(format("The water looks so nice!\n"
                    "But I'm not allowed to swim yet…"), type=MSGBOX_NPC)
}

text LumenVillage_Text_PlayersHouse_Sign {
    format("{PLAYER}'s House")
}

script LumenVillage_EventScript_ProfMyraApproachesPlayerInSpotA {
    if (var(VAR_INTRO_SCENE) == 5) {
        lock
        setvar(VAR_INTRO_SCENE, 6)
        setflag(FLAG_DELIVERED_JIRACHI_MEDICINE)
        removeitem(ITEM_PARCEL)
        hidefollower()
        applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark delay_16 face_down))
        playse(SE_PIN)
        waitmovement(0)
        clearflag(FLAG_HIDE_MYRA_IN_LUMEN_VILLAGE)
        addobject(LumenVillage_ProfMyra)
        applymovement(LumenVillage_ProfMyra, LumenVillage_Movement_ProfMyraApproachesPlayerInSpotA)
        waitmovement(0)
        applymovement(LOCALID_PLAYER, moves(face_right))
        waitmovement(0)
        call(LumenVillage_EventScript_ProfMyra_Dialogue_After_Jirachi_Dialogue)
        releaseall
    }
}

script LumenVillage_EventScript_ProfMyraApproachesPlayerInSpotB {
    if (var(VAR_INTRO_SCENE) == 5) {
        lock
        setvar(VAR_INTRO_SCENE, 6)
        setflag(FLAG_DELIVERED_JIRACHI_MEDICINE)
        removeitem(ITEM_PARCEL)
        hidefollower()
        applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark delay_16 face_down))
        playse(SE_PIN)
        waitmovement(0)
        clearflag(FLAG_HIDE_MYRA_IN_LUMEN_VILLAGE)
        addobject(LumenVillage_ProfMyra)
        applymovement(LumenVillage_ProfMyra, LumenVillage_Movement_ProfMyraApproachesPlayerInSpotB)
        waitmovement(0)
        call(LumenVillage_EventScript_ProfMyra_Dialogue_After_Jirachi_Dialogue)
        releaseall
    }
}

script LumenVillage_EventScript_ProfMyraApproachesPlayerInSpotC {
    if (var(VAR_INTRO_SCENE) == 5) {
        lock
        setvar(VAR_INTRO_SCENE, 6)
        setflag(FLAG_DELIVERED_JIRACHI_MEDICINE)
        removeitem(ITEM_PARCEL)
        hidefollower()
        applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark delay_16 face_down))
        playse(SE_PIN)
        waitmovement(0)
        clearflag(FLAG_HIDE_MYRA_IN_LUMEN_VILLAGE)
        addobject(LumenVillage_ProfMyra)
        applymovement(LumenVillage_ProfMyra, LumenVillage_Movement_ProfMyraApproachesPlayerInSpotC)
        waitmovement(0)
        applymovement(LOCALID_PLAYER, moves(face_left))
        waitmovement(0)
        call(LumenVillage_EventScript_ProfMyra_Dialogue_After_Jirachi_Dialogue)
        releaseall
    }
}

script LumenVillage_EventScript_ProfMyraApproachesPlayerInSpotD {
    if (var(VAR_INTRO_SCENE) == 5) {
        lock
        setvar(VAR_INTRO_SCENE, 6)
        setflag(FLAG_DELIVERED_JIRACHI_MEDICINE)
        removeitem(ITEM_PARCEL)
        hidefollower()
        applymovement(LOCALID_PLAYER, moves(emote_exclamation_mark delay_16 face_down))
        playse(SE_PIN)
        waitmovement(0)
        clearflag(FLAG_HIDE_MYRA_IN_LUMEN_VILLAGE)
        addobject(LumenVillage_ProfMyra)
        applymovement(LumenVillage_ProfMyra, LumenVillage_Movement_ProfMyraApproachesPlayerInSpotD)
        waitmovement(0)
        applymovement(LOCALID_PLAYER, moves(face_left))
        waitmovement(0)
        call(LumenVillage_EventScript_ProfMyra_Dialogue_After_Jirachi_Dialogue)
        releaseall
    }
}

script LumenVillage_EventScript_ProfMyra_Dialogue_After_Jirachi_Dialogue {
    lockall
    speakername(ProfMyraName)
    msgbox(format("{PLAYER}! Wait!\p"
        "Did you find Jirachi? Is it safe?\p"
        "…\p"
        "You… you spoke to it…? And it… it told you all of that??\p"
        "…\p"
        "…I see. This is incredible, {PLAYER}.\p"
        "You must be truly special to be able to speak to Jirachi directly.\p"
        "It sounds as if this situation is bigger than all of us.\p"
        "Jirachi needs your help, and you're the only one it trusts right now.\p"
        "If you're really going to do this - travel the region, help it find its friend…\l"
        "then you'll need this.\p"
        "This is a Pokédex, a device that records data on all Pokémon you encounter."))
    playfanfare(MUS_OBTAIN_ITEM)
    speakername(NULL)
    msgbox(format("{PLAYER} received the Pokédex!"))
    waitfanfare()
    setflag(FLAG_SYS_POKEDEX_GET)
    special(SetUnlockedPokedexFlags)
    speakername(ProfMyraName)
    msgbox(format("And take these Pokéballs - you'll need them."))
    speakername(NULL)
    giveitem(ITEM_POKE_BALL, 5)
    speakername(ProfMyraName)
    msgbox(format("I won't stop you, {PLAYER}… but please be careful.\p"
        "You're walking into something vast and dangerous.\p"
        "Use the items I gave you to get stronger. You'll need all the help you can get to help Jirachi.\p"
        "…Before you go, why don't you stop home and say goodbye to your mother?\p"
        "I know she'll want to see you off.\p"
        "And of course, if you ever need anything, feel free to come back here.\p"
        "I wish you all the luck on your journey, {PLAYER}!"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_PLAYER, moves(delay_16 face_down))
    applymovement(LumenVillage_ProfMyra, moves(walk_down * 7))
    waitmovement(0)
    setflag(FLAG_HIDE_MYRA_IN_LUMEN_VILLAGE)
    removeobject(LumenVillage_ProfMyra)
    clearflag(FLAG_HIDE_MYRA_IN_LAB)
    releaseall
}

movement LumenVillage_Movement_ProfMyraApproachesPlayerInSpotA {
    walk_fast_up * 7
    face_left
}

movement LumenVillage_Movement_ProfMyraApproachesPlayerInSpotB {
    walk_fast_up * 6
}

movement LumenVillage_Movement_ProfMyraApproachesPlayerInSpotC {
    walk_fast_up * 7
    face_right
}

movement LumenVillage_Movement_ProfMyraApproachesPlayerInSpotD {
    walk_fast_up * 7
    walk_fast_right
    face_right
}